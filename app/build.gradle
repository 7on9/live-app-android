buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.Mindera:gradle-slack-plugin:1.0.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.mindera.gradle.slack'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://hypertrack-android-sdk.s3-website-us-west-2.amazonaws.com/' }
    maven { url "https://repo.eclipse.org/content/repositories/paho-releases/" }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    lintOptions {
        abortOnError false
    }
    def versionPropsFile = file('gradle.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def value = 0

        def runTasks = gradle.startParameter.taskNames
        if ('cUDD' in runTasks || 'crashlyticsUploadDistributionDebug' in runTasks
                || 'cUDR' in runTasks || 'crashlyticsUploadDistributionRelease' in runTasks) {
            value = 1;
        }

        def versionMajor = 1
        def versionMinor = 2
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_NUMBER'] = versionNumber.toString()
        versionProps['version'] = versionMajor + "." + versionMinor + "." + versionPatch
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "io.hypertrack.sendeta"
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            minSdkVersion 19
            targetSdkVersion 23
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
        }

    } else {
        throw new GradleException("Could not read version.properties!")
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def newFileName = "SendETA-${variant.versionName}.apk";
            def apk = output.outputFile;
            output.outputFile = new File(apk.parentFile, newFileName);
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_KEY", '"pk_e956d4c123e8b726c10b553fe62bbaa9c1ac9451"'
            buildConfigField "String", "BASE_URL", '"https://api.sendeta.com"'
            buildConfigField "String", "HYPERTRACK_BASE_URL", '"https://app.hypertrack.io/api/v1/"'
            buildConfigField "String", "HYPERTRACK_API_KEY", '"sk_35b9d87cba7ca206bcb7a06d5c94b24a58cdaac3"'
            resValue "string", "eta_link_url", "www.eta.fyi"
            resValue "string", "facebook_app_id", "1544357779206506"
            ext.betaDistributionReleaseNotes = "BETA distribution via Gradle"
            ext.betaDistributionEmails = "ulhas@hypertrack.io, piyush@hypertrack.io"
        }
        debug {
            debuggable true
            versionNameSuffix '-debug'
            applicationIdSuffix ".debug"
            manifestPlaceholders = [applicationLabel: "SendETA Debug"]
            buildConfigField "String", "API_KEY", '"pk_03e3176a9831360e162093292049757b130c75cf"'
            buildConfigField "String", "BASE_URL", '"https://meta-api-staging.herokuapp.com/"'
            buildConfigField "String", "HYPERTRACK_BASE_URL", '"https://hypertrack-api-v2-staging.herokuapp.com/api/v1/"'
            buildConfigField "String", "HYPERTRACK_API_KEY", '"sk_c98f8d3026a97c4a130161240dd886616959d9e8"'
            resValue "string", "eta_link_url", "webtracking-staging.herokuapp.com"
            resValue "string", "facebook_app_id", "1548496698792614"
            ext.betaDistributionReleaseNotes = "BETA distribution via Gradle"
            ext.betaDistributionEmails = "ulhas@hypertrack.io, piyush@hypertrack.io"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.2.3'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.github.tajchert:WaitingDots:0.3.2'
    compile 'com.google.android.gms:play-services-gcm:9.6.1'
    compile 'com.google.android.gms:play-services-places:9.6.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'

    debugCompile(project(path: ':consumer', configuration: 'debug')) {
        transitive = true
    }
    debugCompile(project(path: ':transmitter', configuration: 'debug')) {
        transitive = true
    }

    releaseCompile(project(path: ':consumer', configuration: 'release')) {
        transitive = true
    }
    releaseCompile(project(path: ':transmitter', configuration: 'release')) {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'